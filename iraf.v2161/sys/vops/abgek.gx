# Copyright(c) 1986 Association of Universities for Research in Astronomy Inc.

# ABGEK -- Vector boolean greater than or equals constant.  C[i], type INT,
# is set to 1 if A[i] is greater than or equal to B, else C[i] is set to zero.

procedure abgek$t (a, b, c, npix)

PIXEL	a[ARB]
PIXEL	b
int	c[ARB]
int	npix
int	i
$if (datatype == x)
real	abs_b
$endif

begin
	# The case b==0 is perhaps worth optimizing.  On many machines this
	# will save a memory fetch.

	if (b == 0$f) {
	    $if (datatype == x)
	    call amovki (1, c, npix)
	    $else
	    do i = 1, npix
		if (a[i] >= 0)
		    c[i] = 1
		else
		    c[i] = 0
	    $endif
	} else {
	    $if (datatype == x)
	    abs_b = abs (b)
	    $endif
	    do i = 1, npix
		$if (datatype == x)
		if (abs (a[i]) >= abs_b)
		$else
		if (a[i] >= b)
		$endif
		    c[i] = 1
		else
		    c[i] = 0
	}
end
