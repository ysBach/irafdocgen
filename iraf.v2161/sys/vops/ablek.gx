# Copyright(c) 1986 Association of Universities for Research in Astronomy Inc.

# ABLEK -- Vector boolean less than or equals constant.  C[i], type INT,
# is set to 1 if A[i] is less than or equal to B, else C[i] is set to zero.

procedure ablek$t (a, b, c, npix)

PIXEL	a[ARB]
PIXEL	b
int	c[ARB]
int	npix
int	i
$if (datatype == x)
real	abs_b
$endif

begin
	# The case b==0 is perhaps worth optimizing.  On many machines this
	# will save a memory fetch.

	if (b == 0$f) {
	    do i = 1, npix
		$if (datatype == x)
		if (abs (a[i]) == 0)
		$else
		if (a[i] <= 0)
		$endif
		    c[i] = 1
		else
		    c[i] = 0
	} else {
	    $if (datatype == x)
	    abs_b = abs (b)
	    $endif
	    do i = 1, npix
		$if (datatype == x)
		if (abs (a[i]) <= abs_b)
		$else
		if (a[i] <= b)
		$endif
		    c[i] = 1
		else
		    c[i] = 0
	}
end
