# Copyright(c) 1986 Association of Universities for Research in Astronomy Inc.

# ALLN -- Compute the natural logarithm of a vector (generic).  If the natural
# logarithm is undefined (x <= 0) a user supplied function is called to get
# the pixel value to be returned.

procedure alln$t (a, b, npix, errfcn)

PIXEL	a[ARB], b[ARB]
int	npix, i
extern	errfcn()
PIXEL	errfcn()
errchk	errfcn

begin
	do i = 1, npix {
	    $if (datatype == x)
		if (a[i] == 0$f)
	    $else
		if (a[i] <= 0$f)
	    $endif
		    b[i] = errfcn (a[i])
		else {
		    $if (datatype == si)
			b[i] = log (real (a[i]))
		    $else $if (datatype == l)
			b[i] = log (double (a[i]))
		    $else
			b[i] = log (a[i])
		    $endif $endif
		}
	}
end
